'_____________________________________________________________________________
'
' ANIMATION OBJECTS
'_____________________________________________________________________________
FUNCTION Add_Aobj
    REDIM _PRESERVE Aobjs(UBOUND(AObjs) + 1) AS Aobj

    Add_Aobj = UBOUND(AObjs)
    Aobjs(Add_Aobj).obj = Add_Aobj
END FUNCTION

SUB Update_Aobjs
    DIM i: FOR i = 1 TO UBOUND(AObjs)
        Update_Anim Aobjs(i)
    NEXT i
END SUB

SUB Draw_Aobjs
    DIM AObj, spr&, x, y, w, h
    DIM i: FOR i = 1 TO UBOUND(AObjs)
        AObj = Aobjs(i).obj
        spr& = Sprites(Aobjs(i).spr).spr
        x = Objs(AObj).x
        y = Objs(AObj).y
        w = _WIDTH(spr&)
        h = _HEIGHT(spr&)

        _PUTIMAGE (x, y)-(x + _WIDTH(spr&), y + _HEIGHT(spr&)), spr&
    NEXT i
END SUB

SUB Clear_Aobjs
	Clear_Anims
    REDIM Aobjs(0) AS Aobj
END SUB



'_____________________________________________________________________________
'
' ANIMATIONS
'_____________________________________________________________________________
FUNCTION Add_Anim (dur, path$)
    REDIM _PRESERVE Anims(UBOUND(Anims) + 1) AS Anim

    Add_Anim = UBOUND(Anims)
    Anims(Add_Anim).dur = dur
    Anims(Add_Anim).spr = UBOUND(Sprites) + 1

    DIM i, spr&: i = 0

    DO
        spr& = add_sprite&(path$, i)
        i = i + 1
    LOOP WHILE spr& < -1

    Anims(Add_Anim).frames = i - 1

    IF DEBUG = TRUE THEN SLEEP
END FUNCTION

SUB Update_Anim (Aobj AS Aobj)
    DIM Anim AS Anim: Anim = Anims(Aobj.anim)

    IF Aobj.t_anim > 0 THEN
        Aobj.t_anim = Aobj.t_anim - 1
    ELSE
        Aobj.t_anim = Anim.dur
        Aobj.spr = Aobj.spr + 1

        IF Aobj.spr >= Anim.spr + Anim.frames THEN
            Aobj.spr = Anim.spr
        END IF
    END IF

    IF DEBUG = TRUE THEN LOCATE 1, 1: PRINT Aobj.spr
END SUB

SUB Start_Anim (Aobj AS Aobj, i)
    DIM Anim AS Anim: Anim = Anims(i)

    Aobj.anim = i
    Aobj.t_anim = Anim.dur
    Aobj.spr = Anim.spr
END SUB

SUB Play_Anim (Aobj AS Aobj, i)
    IF Aobj.anim = i THEN EXIT SUB
    Start_Anim Aobj, i
END SUB

SUB Clear_Anims
	Clear_Sprites
    REDIM Anims(0) AS Anim
END SUB



'_____________________________________________________________________________
'
' SPRITES
'_____________________________________________________________________________
FUNCTION add_sprite& (path$, i)
	dim i$:	i$ = LTRIM$(STR$(i))
    add_sprite& = _LOADIMAGE(path$ + i$ + ".png")

    IF add_sprite& < -1 THEN
        REDIM _PRESERVE Sprites(UBOUND(Sprites) + 1) AS Sprite
        Sprites(UBOUND(Sprites)).spr = add_sprite&

        IF DEBUG = TRUE THEN PRINT path$ + i$ + " -> " + LTRIM$(STR$(UBOUND(Sprites)))
    END IF
END FUNCTION

SUB Clear_Sprites
    DIM i: FOR i = 1 TO UBOUND(Sprites)
        IF Sprites(i).spr < -1 THEN _FREEIMAGE Sprites(i).spr
    NEXT i

    REDIM Sprites(0) AS Sprite
END SUB
